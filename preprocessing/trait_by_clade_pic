#!/usr/bin/env perl

# trait_by_clade_pic
# Copyright 2012 Florent Angly
# You may distribute this module under the terms of the GPLv3


=head1 NAME

trait_by_clade_pic - Summarize a trait at different taxonomic levels using
phylogenetic independent contrasts

=head1 SYNOPSIS

  trait_by_clade_pic -f trait.txt -p gg.tree -t gg_taxo.txt > trait_summary.txt

=head1 DESCRIPTION

This script takes a trait in different species and estimates the trait value
at different taxonomic levels using the phylogenetic independent contrasts
method: Felsenstein, "Phylogenies and the comparative method" (1985)

=head1 REQUIRED ARGUMENTS

=over

=item -i <input_file>

Input file of tab-delimited traits. This file should have been generated by the
script data_combiner. This script will use the column called 'GG ID' and the
one specified by the option <trait_name>.

=for Euclid:
   input_file.type: readable

=item -p <phylo_tree>

Input Greengenes phylogenetic tree in Newick format. Note that quoted named nodes
like '100:s__Bacteroides dorei' will be converted to 's__Bacteroides dorei'

=for Euclid:
   phylo_tree.type: readable

=item -t <taxo_file>

Input Greengenes taxonomy file.

=for Euclid:
   taxo_file.type: readable

=back

=head1 OPTIONAL ARGUMENTS

=over

=item -c <clip_tree>

Save the pruned tree in the given filename. Also, if the file already exists,
re-use it to skip the pruning step. Default: clip_tree.default

=for Euclid:
   clip_tree.type: writable
   clip_tree.default: 'pruned_tree.nw'

=item -n <trait_name>

If the input file contains several traits, select the name of the trait to use
(case-insensitive), e.g. '16S rRNA Count' or 'Genome Size'. Make sure this
matches the name of the trait as found in the input file. Default: trait_name.default

=for Euclid:
   trait_name.type: string
   trait_name.default: '16S rRNA Count'

=back

=head1 AUTHOR

Florent Angly <florent.angly@gmail.com>

=head1 BUGS

All complex software has bugs lurking in it, and this program is no exception.
If you find a bug, please report it on the bug tracker:
L<http://github.com/fangly/AmpliCopyrighter/issues>

=head1 COPYRIGHT

Copyright 2012 Florent Angly

Copyrighter is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.
Copyrighter is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.
You should have received a copy of the GNU General Public License
along with Copyrighter.  If not, see <http://www.gnu.org/licenses/>.

=cut


use strict;
use warnings;
use Method::Signatures;
use Getopt::Euclid qw(:minimal_keys);
use Statistics::Basic qw(mean);
use Bio::Phylo::IO;
use Bio::Community::TaxonomyUtils;
use FindBin qw($Bin);
use lib "$Bin";
use PreprocUtils;

trait_by_clade_pic( $ARGV{'i'}, $ARGV{'p'}, $ARGV{'t'}, $ARGV{'c'}, $ARGV{'n'} );
exit;

func trait_by_clade_pic ( $input_file, $phylo_tree, $taxo_file, $clip_tree, $trait_name ) {

   # Read IMG metadata and extract gg IDs and the desired trait value
   my $id2trait = read_metadata($input_file, $trait_name);

   # Make average for GG IDs that have several trait values (several genomes)
   $id2trait = average_by_key($id2trait);

   # Our very own, special GG tree
   my $tree = grow_tree($phylo_tree, $clip_tree, [keys %$id2trait], $id2trait);

   # Calculate interior node trait value using PIC
   $id2trait = pic($tree, $id2trait);

   # Read GG taxonomy and convert GG IDs to GG taxonomy string
   my $gg_taxonomy = PreprocUtils::read_lookup($taxo_file);
   my $tax2trait = convert_id_to_tax($id2trait, $gg_taxonomy);

   # Do average for each taxonomy string
   $tax2trait = average_by_key($tax2trait);

   #### need to add trait value for taxonomic strings that were pruned

   # Print results
   print_results($tax2trait, $trait_name);

   return 1;
}


func print_results ($tax2trait, $trait_name) {
   # Print results, sorted by taxonomic level and alphabetically within each level
   # Sort results by taxonomic level
   my $max_level = 0;
   for my $tax_str (keys %$tax2trait) {
      # Split and clean taxonomy string to find taxonomic level
      my $lineage_arr = Bio::Community::TaxonomyUtils::split_lineage_string( $tax_str );
      my $level = scalar @$lineage_arr;
      if ($level > $max_level) {
         $max_level = $level;
      }
      # Re-assemble (cleaned) taxonomy string
      my $clean_tax_str = Bio::Community::TaxonomyUtils::get_lineage_string( $lineage_arr );
      $tax2trait->{$level}->{$clean_tax_str} = $tax2trait->{$tax_str};
      delete $tax2trait->{$tax_str};
   }
   # Print results
   print "# taxonomy\t$trait_name\n";
   for my $level (1 .. $max_level) {
      my $entries = $tax2trait->{$level};
      for my $tax ( sort keys %$entries ) {
         my $val = $entries->{$tax};
         print "$tax\t$val\n";
      }
      print "\n";
   }
   return 1;
}


func convert_id_to_tax ($id_hash, $gg_taxonomy) {
   my $tax_hash;
   while ( my ($id, $val) = each %$id_hash ) {
      my $tax;
      if ( is_id($id) ) {
         # Convert GG ID to GG full taxonomic string
         $tax = $gg_taxonomy->{$id};
         if (not defined $tax) {
            warn "Warn: Missing GG taxonomy for GG ID '$id'\n";
            next;
         }
      } else {
         # Given string is already a GG full taxonomic string
         $tax = $id;
      }
      $tax_hash->{$tax} = $val;
   }
   return $tax_hash;
}


func pic ($tree, $id2trait) {
   # Use phylogenetic independent contrasts method to estimate trait value for
   # interior nodes of given $tree and save this in $id2trait
   # Note: We're irremediably altering the given tree!
   
   warn "Info: Applying phylogenetic independent contrasts to tree\n";
   my $nonzero_length = 1e-5;

   while ($tree->calc_number_of_cherries > 0) {
      my $cherries = get_cherries($tree);
      while (my ($uid, $nodes) = each %$cherries) {
         # Calculate trait value for parent $pval as a weighted average of the
         # children's trait value @cvals where the weights $weis are proportional
         # to the inverse of the branch distance @dists between parent and
         # children. Hence, the trait value of a child closer to the parent
         # contributes more than the trait value of a childr that is farther away.
         my ($parent, @children) = @$nodes;
         my @cvals  = map { $_->get_generic('trait') } @children;
         my @cdists = map { 0+$_->get_branch_length || $nonzero_length } @children;
         my @weis   = map { 1 / $_ } @cdists;
         my $pval = ($cvals[0]*$weis[0] + $cvals[1]*$weis[1]) / ($weis[0]+$weis[1]);
         $parent->set_generic('trait' => $pval);

         print_cherry($nodes); ####

         # Delete children
         $parent->prune_child($children[0]);
         $parent->prune_child($children[1]);

         # Stretch parent's branch
         my $pdist = $parent->get_branch_length;
         if ( ($cdists[0] > 0) && ($cdists[1] > 0) ) {
            $pdist += $cdists[0]*$cdists[1] / ($cdists[0]+$cdists[1]);
         }
         $parent->set_branch_length($pdist);

         # Save parent's trait value
         my $pid = get_clean_name($parent); # GG ID or full taxonomic string
         if ( $pid && (exists $id2trait->{$pid}) ) {
            my $oval = $id2trait->{$pid}; # original trait value
            my $uid  = $parent->get_id;   # unique node identifier
            my $msg = "Parent '$pid' (uid $uid) already has a trait value ($oval)";
            if ($id2trait->{$pid} == $pval) {
               warn "Warn: $msg, but with the same value. That may be alright.\n";
            } else {
               die "Error: $msg that is different from the value to set ($pval)\n";
            }
         } else {
            $id2trait->{$pid} = $pval;
         }

      }
   }

   my $nof_leaves = $tree->calc_number_of_terminals;
   if ($nof_leaves == 1) {
      # Print trait value for remaining node
      my $last_node = @{$tree->get_entities}[0];
      my $last_val  = $last_node->get_generic('trait');
      warn "Info: Last tree node had trait value $last_val\n";
   } else {
      # Error
      my $out_file = 'tree_debug.nw';
      write_tree($tree, $out_file);
      die "Error: Internal problem. There should be only one node left but there ".
          "are still $nof_leaves leaves. Wrote what's left of the tree in the Newick ".
          "file $out_file for debugging\n";
   }

   return $id2trait;
}


func get_cherries ($tree) {
   # Find the cherries in the tree and report them in a hash, keyed by the ID of
   # their parent
   my %cherries;
   for my $tip ( @{ $tree->get_terminals } ) {
      if ( my $parent = $tip->get_parent ) {
         if ( $parent->is_preterminal ) {
            my $children = $parent->get_children;
            if ( scalar @{$children} == 2 ) {
               warn "CHERRY: P ".get_clean_name($parent)." C1 ".get_clean_name($children->[0])." C2 ".get_clean_name($children->[1])."\n";
               $cherries{ $parent->get_id } = [$parent, @$children];
            }
         }
      }
   }
   return \%cherries;
}


func print_cherry ($cherry) {
   my ($parent, @children) = @$cherry;
   my $msg = 
      "CHILDREN: ".
      (get_clean_name($children[0])||'?')."(uid ".$children[0]->get_id.")=".$children[0]->get_generic('trait')."  ".
      (get_clean_name($children[1])||'?')."(uid ".$children[1]->get_id.")=".$children[1]->get_generic('trait')."\n".
      "PARENT: ".
      (get_clean_name($parent)||'?')."(uid ".$parent->get_id.")=".$parent->get_generic('trait')."\n\n";
   warn $msg;
   return 1;
}


func write_tree ($tree, $file) {
   open my $out, '>', $file or die "Error: Could not write file $file\n$!\n";
   print $out Bio::Phylo::IO->unparse( -phylo => $tree, -format => 'newick' );
   close $out;
   return 1;
}


func grow_tree ($tree_file, $clip_file, $ids, $id2trait) {
   # Parse Newick tree, prune it and attach trait

   # Parse Newick tree
   my $tree;
   my $num_leaves;
   if ((defined $clip_file) && (-f $clip_file)) {
      warn "Info: Parsing already-pruned tree $clip_file ...\n";
      $tree = Bio::Phylo::IO->parse(
         -file   => $clip_file,
         -format => 'newick',
      )->first;
      $num_leaves = $tree->calc_number_of_terminals();
      warn "Info: Saw $num_leaves tree leaves\n";
   } else {
      warn "Info: Cleaning tree $tree_file ...\n";
      my $tree_string = clean_newick($tree_file);

      print_tree($tree_string, 'debug1.nw');        ###

      warn "Info: Parsing tree...\n";
      $tree = Bio::Phylo::IO->parse(
         -string => $tree_string,
         -format => 'newick',
         -keep   => $ids, # remove some of the nodes we won't need
      )->first;
      warn "Info: Saw ".$tree->calc_number_of_terminals()." tree leaves\n";

      print_tree($tree, 'debug2.nw');               ###
      print_tree_nodes($tree, 'debug2_leaves.txt'); ###

      warn "Info: Pruning tree...\n";
      $tree->keep_tips($ids);
      $num_leaves = $tree->calc_number_of_terminals;
      warn "Info: $num_leaves tree leaves left\n";

      print_tree($tree, 'debug3.nw');               ###
      print_tree_nodes($tree, 'debug3_leaves.txt'); ###

      # Write pruned tree if requested
      if ($clip_file) {
         warn "Info: Saving tree to $clip_file ...\n";
         write_tree($tree, $clip_file);
      }
   }

   my $num_ids = scalar keys $ids;
   if ( $num_ids != $num_leaves ) {
      warn "Warn: Looks like the tree is missing ".($num_ids-$num_leaves)." GG IDs\n";
   }

   # Attach trait to nodes
   for my $leaf ( @{$tree->get_terminals} ) {
      my $gg_id = get_clean_name($leaf);
      my $val = $id2trait->{$gg_id} ||
         die "Error: Could not find trait value for tree leaf with GG ID $gg_id\n";
      #warn "Info: Setting trait of GG ID $gg_id (uID ".$leaf->get_id.") to $val\n";
      $leaf->set_generic( 'trait' => $val );
   }

   return $tree;
}


func print_tree_nodes ($tree, $file) {
   # Given a Bio::Phylo::Forest::Tree, write the name of its node in the
   # specified file name.
   open my $out, '>', $file or die "Error: Could not write file $file\n$!\n";
   for my $node (@{$tree->get_entities}) {
      print $out get_clean_name($node)."\n";
   }
   close $out;
   return 1;
}


func print_tree ($tree, $file) {
   # Given a Bio::Phylo::Forest::Tree, write its Newick representation in the
   # specified file name. Alternatively, you can pass a Newick string directly.
   my $tree_string;
   if ( ref($tree) eq 'Bio::Phylo::Forest::Tree' ) {
      $tree_string = $tree->as_string;
   } else {
      $tree_string = $tree;
   }
   open my $out, '>', $file or die "Error: Could not write file $file\n$!\n";
   print $out $tree_string;
   close $out;
   return 1;
}


func clean_newick ($file) {
   # Take a tree in a Newick file, cleans quoted node names like '100:s__Bacteroides
   # dorei' to 's__Bacteroides dorei', i.e. keep only the taxonomy name. Need to
   # keep the single-quotes because some taxon names contain semicolons.
   # Actually, also convert semicolons to '.,' because it will confuse the -keep
   # option of Bio::Phylo::IO->parse();
   # Note: no need to replace whitespace by underscore '_'

   #### for gg 2012, need a script to convert GG ID to ID of 99% cluster representative sequence

   my $tree_string;
   open my $in, '<', $file or die "Error: Could not read file $file\n$!\n";
   while (my $line = <$in>) {
      $line =~ s/'(.+?:)?(.*?)'/replace_sc("'$2'")/ge;
      $tree_string .= $line;
   }
   close $in;
   return $tree_string;
}


func replace_sc ($string) {
   # Replace any semicolon in the given string by '.,'
   $string =~ s/;/.,/g;
   return $string;
}


func restore_sc ($string) {
   # Restore any '.,' to semicolon
   $string =~ s/.,/;/g;
   return $string;
}


func get_clean_name ($node) {
   # Get node name, replace semicolons and unquote it. If the name is not an ID
   # but part of a taxonomic string, e.g. 'g__Bacteroides' or 's__Bacteroides salyersiae'
   # reconstruct the full taxonomic string
   my $name = $node->get_name;
   $name = clean_name($name);
   if ( not is_id($name) ) {
      while (1) {
         $node = $node->get_parent || last;
         my $parent_name = clean_name($node->get_name);
         if ( not is_id($parent_name) ) {
            $name = $parent_name.'; '.$name;
         }
      }
   }
   return $name;
}


func is_id ($str) {
   # Returns true is given string contains only numerals 0-9
   return( ($str =~ m/[^0-9]/) ? 0 : 1 );
}


func clean_name ($name) {
   $name = restore_sc($name);
   $name =~ s/^'(.*)'$/$1/;
   $name =~ s/^"(.*)"$/$1/;
   return $name;
}


func read_metadata ( $file, $trait_name ) {
   my %id2trait;
   open my $fh, '<', $file or die "Error: Could not read file $file\n$!\n";
   my $header = <$fh>;
   chomp $header;
   my @cols = PreprocUtils::find_column_for($header, 'GG ID', $trait_name);
   warn "Info: Found trait '$trait_name' in column ".($cols[-1]+1)." of input ".
        "file $file\n";
   # Parse input file
   my $num = 0;
   while (my $line = <$fh>) {
      chomp $line;
      my ($gg_id, $trait_val) = (split /\t/, $line)[@cols];
      if ( (not defined $gg_id) || ($gg_id eq '-') ) {
         next;
      }
      if ( (not defined $trait_val) || ($trait_val eq '-') ) {
         next;
      }
      push @{$id2trait{$gg_id}}, $trait_val;
      $num++;
   }
   close $fh;
   warn "Info: Read $num entries with a GG ID and a trait value\n";
   return \%id2trait;
}


func average_by_key ( $hash ) {
   for my $key (keys %$hash) {
      $hash->{$key} = mean($hash->{$key})->query;
   }
   return $hash;
}
